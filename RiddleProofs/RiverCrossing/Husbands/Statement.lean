import Init.WF
import Mathlib.Data.Finset.Basic
import Mathlib.Data.Fintype.Basic
import RiddleProofs.RiverCrossing.Basic

namespace RiverCrossing.Husbands

open RiverBank

/-!
# Problem statement: The jealous husbands puzzle

```
Left Bank                    River                    Right Bank
-----------------------------------------------------------------
Hâ‚ Wâ‚ Hâ‚‚ Wâ‚‚ Hâ‚ƒ Wâ‚ƒ    [=======ðŸš¤======]               (empty)
```

Three husbands (Hâ‚, Hâ‚‚, Hâ‚ƒ) and their wives (Wâ‚, Wâ‚‚, Wâ‚ƒ) must cross a river using a small boat.

## The rules (constraints)

1. **Boat capacity**: The boat can carry at most 2 people at a time
2. **Boat operation**: The boat cannot cross the river by itself (someone must operate it)
3. **Jealousy constraint**: A wife cannot be alone with another husband unless her own husband is present

The **jealousy constraint** is the key challenge. For example:
- âŒ Wâ‚ and Hâ‚‚ cannot be together without Hâ‚ present
- âœ… Wâ‚, Hâ‚, and Hâ‚‚ can be together (Hâ‚ protects his wife)
- âœ… Hâ‚ and Hâ‚‚ can be alone together (no wives involved)


## Challenges

- Can you generalize to N couples?
- What about more than 2 people in a boat?
- Can you optimize my BFS in `Search.lean`?
- Like `JealousMathematician.lean`, can you write a version for "cannibals and missionaries"?

-/

/-- Number of couples in the puzzle -/
def num_couples : Nat := 3

open RiverBank

/-- A person is either a husband or wife, identified by their couple index.
    For example: `husband âŸ¨0, _âŸ©` represents the first husband (Hâ‚€). -/
inductive Person
| husband (i : Fin num_couples)
| wife (i : Fin num_couples)
deriving DecidableEq, Repr



/-- Extract the couple identifier from a person.
    Both husband and wife of couple i have couple_id = i. -/
def Person.couple_id : Person â†’ Fin num_couples
| .husband i => i
| .wife i => i

/-- The complete state of the puzzle: boat location + position of all people.

    Example state representation:
    - `boat = left`: The boat is on the left bank
    - `husbands = [left, right, left]`: Hâ‚€ and Hâ‚‚ on left, Hâ‚ on right
    - `wives = [left, left, right]`: Wâ‚€ and Wâ‚ on left, Wâ‚‚ on right -/
abbrev JealousHusbandsState := RiverCrossingState Person Person num_couples

instance : BEq JealousHusbandsState where
  beq s1 s2 := s1.boat == s2.boat &&
               s1.entities_a.toList == s2.entities_a.toList &&
               s1.entities_b.toList == s2.entities_b.toList

/-- Get the current bank location of a person in the given state. -/
def Person.bank (p : Person) (s : JealousHusbandsState) : RiverBank :=
match p with
| .husband i => s.entities_a.get i
| .wife i => s.entities_b.get i

/-- Helper function to check if a wife is alone with another husband.
    Returns true if wife i is on the same bank as husband j, but husband i is not. -/
def wife_alone_with_other_husband (s : JealousHusbandsState) (wife_i : Fin num_couples) (husband_j : Fin num_couples) : Bool :=
  let wife_bank := s.entities_b[wife_i]!
  let other_husband_bank := s.entities_a[husband_j]!
  let own_husband_bank := s.entities_a[wife_i]!
  wife_bank = other_husband_bank && own_husband_bank â‰  other_husband_bank

/-- Checks if a state satisfies the jealousy constraint.-/
def bank_safe (s : JealousHusbandsState) : Bool :=
  let pairs : List (Fin num_couples) := List.finRange num_couples
  pairs.all (fun wife_i =>
    pairs.all (fun husband_j =>
      wife_i = husband_j || !wife_alone_with_other_husband s wife_i husband_j))

def state_safe_prop (s : JealousHusbandsState) : Prop := bank_safe s = true

instance : DecidablePred state_safe_prop := by
  unfold state_safe_prop
  infer_instance

def jealous_husbands_initial_state : JealousHusbandsState :=
  initial_state Person Person num_couples

def jealous_husbands_final_state : JealousHusbandsState :=
  final_state Person Person num_couples

theorem final_safe : bank_safe jealous_husbands_final_state = true := by
  unfold bank_safe jealous_husbands_final_state
  decide

instance {n: Nat} : OfNat (Fin num_couples) n := mkFinOfNat num_couples (by decide)

end RiverCrossing.Husbands
